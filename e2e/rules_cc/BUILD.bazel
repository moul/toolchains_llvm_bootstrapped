load("@bazel_lib//lib:transitions.bzl", "platform_transition_binary")
load("@platforms//host:constraints.bzl", "HOST_CONSTRAINTS")
load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_cc//cc:cc_static_library.bzl", "cc_static_library")
load("@rules_cc//cc:cc_test.bzl", "cc_test")
load("@rules_shell//shell:sh_test.bzl", "sh_test")
load("@toolchains_llvm_bootstrapped//:defs.bzl", "exec_test")
load(":defs.bzl", "opt_binary", "ubsan_cc_binary")

opt_binary(
    name = "no_optimize",
    copts = ["-O0"],
    srcs = ["no_optimize.cc"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

opt_binary(
    name = "optimize",
    srcs = ["optimize.cc"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

# dynamic linker + dynamic runtime libs
cc_binary(
    name = "main_dynamic",
    srcs = ["main.cc"],
    linkstatic = False,
    target_compatible_with = select({
        # We do not support musl libc.so
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "@toolchains_llvm_bootstrapped//constraints/libc:musl": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

# dynamic linker + static runtime libs
cc_binary(
    name = "main_default",
    srcs = ["main.cc"],
)

# same as default
cc_binary(
    name = "main_static",
    srcs = ["main.cc"],
    linkstatic = True,
)

# dynamic linker + static runtime libs (even tho -static)
# but because musl adds -static-pie, no dynamic linker at all.
cc_binary(
    name = "main_fully_static",
    srcs = ["main.cc"],
    linkstatic = True,
    features = ["fully_static_link"],
    target_compatible_with = select({
        # only musl supports fully static linking.
        "@toolchains_llvm_bootstrapped//constraints/libc:musl": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

# Implies dynamic_runtime_libs
cc_test(
    name = "test",
    srcs = ["main.cc"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        # We do not support musl libc.so
        "@toolchains_llvm_bootstrapped//constraints/libc:musl": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    exec_compatible_with = HOST_CONSTRAINTS,
    # remote-exec tests can't run without forcing the target platform to match
    # the remote exec platform.
    tags = ["no-remote"],
)

cc_binary(
    name = "main",
    srcs = ["main.cc"],
    # We provide these libs explicitly via the toolchain runtime_library.
    # These options are all no-ops as they resolve to empty stubs.
    # This is basically a test that we can properly ignore these opts.
    linkopts = [
        "-lc++",
        "-lc++abi",
        "-lunwind",
    ],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

# TODO(zbarsky): Turn more of this into a macro once we have more sanitizers?
ubsan_cc_binary(
    name = "ubsan_fail",
    srcs = ["ubsan_fail.cc"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

exec_test(
    sh_test,
    name = "ubsan_output_test",
    srcs = ["ubsan_output_test.sh"],
    data = [":ubsan_fail"],
    env = {"BINARY": "$(rootpath :ubsan_fail)"},
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

# Test that this toolchain properly supports the supports_pic capability.
cc_binary(
    name = "supports_pic_",
    srcs = ["supports_pic.cc"],
    linkshared = True,
    tags = ["manual"],
)

platform_transition_binary(
    name = "supports_pic",
    binary = ":supports_pic_",
    target_platform = "@toolchains_llvm_bootstrapped//platforms:linux_x86_64",
)

cc_test(
    name = "windows_test",
    srcs = ["windows_test.cc"],
    exec_compatible_with = [
        "@platforms//os:windows",
    ],
    target_compatible_with = [
        "@platforms//os:windows",
    ],
    linkopts = [
        "-luuid",
    ],
    # TODO(zbarsky): Tests can't execute remotely from Windows hosts
    tags = ["no-remote"],
)

#######################################################

cc_library(
    name = "duplicate_symbol_a",
    srcs = ["duplicate_symbol_a.cc"],
)

cc_library(
    name = "duplicate_symbol_b",
    srcs = ["duplicate_symbol_b.cc"],
)

cc_static_library(
    name = "duplicate_symbol_lib",
    deps = [
        ":duplicate_symbol_a",
        ":duplicate_symbol_b",
    ],
    tags = ["manual"],
)

sh_test(
    name = "duplicate_static_library_validator_test",
    srcs = ["duplicate_static_library_failure_test.sh"],
    data = [
        "BUILD.bazel",
        "duplicate_symbol_a.cc",
        "duplicate_symbol_b.cc",
    ],
    # TODO(zbarsky): Enable once we ship new prebuilts
    tags = ["manual", "local"],
    size = "large",
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

#######################################################
