load("@bazel_lib//lib:transitions.bzl", "platform_transition_binary")
load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_shell//shell:sh_test.bzl", "sh_test")
load("@toolchains_llvm_bootstrapped//:defs.bzl", "exec_test")
load("//:defs.bzl", "ubsan_cc_binary")

cc_binary(
    name = "main",
    srcs = ["main.cc"],
    # We provide these libs explicitly via the toolchain runtime_library.
    # These options are all no-ops as they resolve to empty stubs.
    # This is basically a test that we can properly ignore these opts.
    linkopts = [
        "-lc++",
        "-lc++abi",
        "-lunwind",
    ],
)

# TODO(zbarsky): Turn more of this into a macro once we have more sanitizers?
ubsan_cc_binary(
    name = "ubsan_fail",
    srcs = ["ubsan_fail.cc"],
)

exec_test(
    sh_test,
    name = "ubsan_output_test",
    srcs = ["ubsan_output_test.sh"],
    data = [":ubsan_fail"],
    env = {"BINARY": "$(rootpath :ubsan_fail)"},
)

# Test that this toolchain properly supports the supports_pic capability.
cc_binary(
    name = "supports_pic_",
    srcs = ["supports_pic.cc"],
    linkshared = True,
    tags = ["manual"],
)

platform_transition_binary(
    name = "supports_pic",
    binary = ":supports_pic_",
    target_platform = "@toolchains_llvm_bootstrapped//platforms:linux_x86_64",
)
