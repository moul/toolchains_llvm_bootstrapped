load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_skylib//rules:common_settings.bzl", "string_setting")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("//toolchain/runtimes:cc_stage0_object.bzl", "cc_stage0_object")
load("//toolchain/args:llvm_target_triple.bzl", "LLVM_TARGET_TRIPLE")
load(":copy_to_resource_directory.bzl", "copy_to_resource_directory")

exports_files(["module_map.BUILD.bazel"])

#TODO(cerisier): duplicate this per runtime repo ? or do differently ?
string_setting(
    name = "linkmode",
    build_setting_default = "dynamic",
    values = [
        "static",
        "dynamic",
    ],
    visibility = ["//visibility:public"],
)

config_setting(
    name = "linkmode_static",
    flag_values = {
        ":linkmode": "static",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "linkmode_dynamic",
    flag_values = {
        ":linkmode": "dynamic",
    },
    visibility = ["//visibility:public"],
)

filegroup(
    name = "none",
    srcs = [],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "static_runtime_lib",
    srcs = select({
        "@platforms//os:linux": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            "@libunwind//:libunwind.static",
        ],
        "@platforms//os:windows": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            "@libunwind//:libunwind.static",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "dynamic_runtime_lib",
    srcs = select({
        "@platforms//os:linux": [
            "@libcxx//:libcxx.shared",
            "@libcxxabi//:libcxxabi.shared",
            "@libunwind//:libunwind.shared",
        ],
        # TODO(zbarsky): windows?
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

cc_library(
    name = "empty",
    srcs = ["empty.c"],
)

cc_stage0_object(
    name = "crti_object",
    srcs = [
        ":empty",
    ],
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    out = "crti.o",
    visibility = ["//visibility:public"],
)

cc_stage0_object(
    name = "crtn_object",
    srcs = [
        ":empty",
    ],
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    out = "crtn.o",
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "crt_objects_directory_linux",
    srcs = select({
        "//platforms/config:musl": [
            # -static
            "//runtimes/musl:musl_crt1.object",
            # static-pie
            "//runtimes/musl:musl_rcrt1.object",
            "//runtimes/musl:musl_Scrt1.object",
        ],
        "//platforms/config:gnu": [
            "//runtimes/glibc:glibc_crt1.object",
            "//runtimes/glibc:glibc_Scrt1.object",
        ],
    }) + [
        ":crti_object",
        ":crtn_object",
        "@compiler-rt//:crtbegin_object",
        "@compiler-rt//:crtbeginS_object",
        "@compiler-rt//:crtbeginT_object",
        "@compiler-rt//:crtend_object",
        "@compiler-rt//:crtendS_object",
    ],
    include_external_repositories = ["**"],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
    root_paths = [
        ".",
        "runtimes/musl",
    ],
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "crt_objects_directory_windows",
    srcs = [
        "//runtimes/mingw:crtbegin.o",
        "//runtimes/mingw:crtend.o",
        "//runtimes/mingw:crt2.o",
        "//runtimes/mingw:dllcrt2.o",
    ],
    target_compatible_with = [
        "@platforms//os:windows",
    ],
    root_paths = [
        "runtimes/mingw",
    ],
    visibility = ["//visibility:public"],
)

alias(
    name = "crt_objects_directory",
    actual = select({
        "@platforms//os:linux": ":crt_objects_directory_linux",
        "@platforms//os:windows": ":crt_objects_directory_windows",
    }),
    visibility = ["//visibility:public"],
)

copy_to_resource_directory(
    name = "resource_directory",
    srcs = {
        "//runtimes/compiler-rt:clang_rt.builtins.static": "libclang_rt.builtins",
    } | select({
        "//config:ubsan_enabled": {
            "//runtimes/compiler-rt:clang_rt.ubsan_standalone.static": "libclang_rt.ubsan_standalone",
            "//runtimes/compiler-rt:clang_rt.ubsan_standalone_cxx.static": "libclang_rt.ubsan_standalone_cxx",
        },
        "//config:msan_enabled": {
            "//runtimes/compiler-rt:clang_rt.msan.static": "libclang_rt.msan",
            "//runtimes/compiler-rt:clang_rt.msan_cxx.static": "libclang_rt.msan_cxx",
        },
        "//config:asan_enabled": {
            "//runtimes/compiler-rt:clang_rt.asan_cxx.static": "libclang_rt.asan_cxx",
            "//runtimes/compiler-rt:clang_rt.asan_static.static": "libclang_rt.asan_static",
            "//runtimes/compiler-rt:clang_rt.asan.static": "libclang_rt.asan",
            "//runtimes/compiler-rt:clang_rt.asan.shared": "libclang_rt.asan",
        },
        "//conditions:default": {},
    }),
    visibility = ["//visibility:public"],
)
