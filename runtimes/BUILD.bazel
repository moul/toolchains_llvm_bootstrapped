load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("//toolchain/runtimes:cc_stage0_object.bzl", "cc_stage0_object")
load("//toolchain/args:llvm_target_triple.bzl", "LLVM_TARGET_TRIPLE")
load(":copy_to_resource_directory.bzl", "copy_to_resource_directory")

filegroup(
    name = "none",
    srcs = [],
    visibility = ["//toolchain:__subpackages__"],
)

filegroup(
    name = "static_runtime_lib",
    srcs = select({
        "@platforms//os:linux": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            ## only if libcxx on linux
            "@libunwind//:libunwind.static",
        ],
        "@platforms//os:windows": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            "@libunwind//:libunwind.static",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//toolchain:__subpackages__"],
)

filegroup(
    name = "dynamic_runtime_lib",
    srcs = select({
        "@platforms//os:linux": [
            "@libcxx//:libcxx.shared",
            "@libcxxabi//:libcxxabi.shared",
            # only if libcxx on linux
            # TODO(zbarsky): Enable this? libunwind depends on a another shared lib which loses GraphNodeInfo provider
            #"@libunwind//:libunwind.shared",
        ],
        # TODO(zbarsky): windows?
        "//conditions:default": [],
    }),
    visibility = ["//toolchain:__subpackages__"],
)

cc_library(
    name = "empty",
    srcs = ["empty.c"],
)

cc_stage0_object(
    name = "crti_object",
    srcs = [
        ":empty",
    ],
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    out = "crti.o",
    visibility = ["//visibility:public"],
)

cc_stage0_object(
    name = "crtn_object",
    srcs = [
        ":empty",
    ],
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    out = "crtn.o",
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "crt_objects_directory_linux",
    srcs = select({
        "//platforms/config:musl": [
            # -static-pie is implied
            "//runtimes/musl:musl_rcrt1.object",
        ],
        "//platforms/config:gnu": [
            "//runtimes/glibc:glibc_Scrt1.object",
            "//runtimes/glibc:glibc_crt1.object",
        ],
    }) + [
        ":crti_object",
        ":crtn_object",
        "@compiler-rt//:crtbegin_object",
        "@compiler-rt//:crtbeginS_object",
        "@compiler-rt//:crtbeginT_object",
        "@compiler-rt//:crtend_object",
        "@compiler-rt//:crtendS_object",
    ],
    include_external_repositories = ["**"],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
    root_paths = [
        ".",
        "runtimes/musl",
    ],
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "crt_objects_directory_windows",
    srcs = [
        "//runtimes/mingw:crtbegin.o",
        "//runtimes/mingw:crtend.o",
        "//runtimes/mingw:crt2.o",
        "//runtimes/mingw:dllcrt2.o",
    ],
    target_compatible_with = [
        "@platforms//os:windows",
    ],
    root_paths = [
        "runtimes/mingw",
    ],
    visibility = ["//visibility:public"],
)

alias(
    name = "crt_objects_directory",
    actual = select({
        "@platforms//os:linux": ":crt_objects_directory_linux",
        "@platforms//os:windows": ":crt_objects_directory_windows",
    }),
    visibility = ["//visibility:public"],
)

copy_to_resource_directory(
    name = "resource_directory",
    srcs = {
        "//runtimes/compiler-rt:clang_rt.builtins.static": "libclang_rt.builtins",
    } | select({
        "//config:ubsan_enabled": {
            "//runtimes/compiler-rt:clang_rt.ubsan.static": "libclang_rt.ubsan_standalone",
        },
        "//conditions:default": {},
    }),
    visibility = ["//visibility:public"],
)
