load("@bazel_lib//lib:copy_file.bzl", "copy_file")
load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_lib//lib:run_binary.bzl", "run_binary")
load("@bazel_skylib//rules:common_settings.bzl", "bool_setting")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@toolchains_llvm_bootstrapped//toolchain/stage2:cc_stage2_library.bzl", "cc_stage2_library")
load("@toolchains_llvm_bootstrapped//toolchain/stage2:cc_stage2_static_library.bzl", "cc_stage2_static_library")
load("//runtimes:defs.bzl", "stub_library")
load(":crt_object.bzl", "crt_object")

MINGW_CRT_COMMON_COPTS = [
    "-D_CRTBLD",
    "-D_WIN32_WINNT=0x0f00",
    "-D__MSVCRT_VERSION__=0x600",
    "-D__USE_MINGW_ANSI_STDIO=0",
    "-fno-builtin",
    "-std=gnu99",
] + select({
    "@platforms//cpu:x86_64": ["-m64"],
    "//conditions:default": [],
}) + [
    "-Wno-typedef-redefinition",
    "-Wno-pragma-pack",
    "-Wno-missing-declarations",
]

cc_stage2_library(
    name = "mingw32_crt_lib",
    srcs = select({
        "@platforms//cpu:x86_64": ["@mingw//:mingw32_crt_x86_64_srcs"],
        "@platforms//cpu:aarch64": ["@mingw//:mingw32_crt_aarch64_srcs"],
    }),
    textual_hdrs = [
        "@mingw//:mingw32_crt_textual_hdrs",
    ],
    # TODO(zbarsky): `includes = ["include"]` instead of mingw_Crt_headers?
    implementation_deps = [
        "@mingw//:mingw_headers",
        "@mingw//:mingw_crt_headers",
    ],
    copts = MINGW_CRT_COMMON_COPTS,
)

cc_stage2_static_library(
    name = "mingw32",
    deps = [":mingw32_crt_lib"],
    visibility = ["//visibility:public"],
)

cc_stage2_library(
    name = "mingwex_crt_lib",
    srcs = select({
        "@platforms//cpu:x86_64": ["@mingw//:mingwex_crt_x86_64_srcs"],
        "@platforms//cpu:aarch64": ["@mingw//:mingwex_crt_aarch64_srcs"],
    }),
    textual_hdrs = select({
        "@platforms//cpu:x86_64": ["@mingw//:mingwex_crt_x86_64_textual_hdrs"],
        "@platforms//cpu:aarch64": ["@mingw//:mingwex_crt_aarch64_textual_hdrs"],
    }),
    implementation_deps = [
        "@mingw//:mingw_headers",
        "@mingw//:mingw_crt_headers",
    ],
    copts = MINGW_CRT_COMMON_COPTS + [
        "-Wno-attribute-warning",
    ],
)

cc_stage2_static_library(
    name = "mingwex",
    deps = [":mingwex_crt_lib"],
)

cc_stage2_library(
    name = "ucrtbase_crt_lib",
    srcs = select({
        "@platforms//cpu:x86_64": ["@mingw//:ucrt_base_crt_x86_64_srcs"],
        "@platforms//cpu:aarch64": ["@mingw//:ucrt_base_crt_aarch64_srcs"],
    }),
    implementation_deps = [
        "@mingw//:mingw_headers",
        "@mingw//:mingw_crt_headers",
    ],
    copts = MINGW_CRT_COMMON_COPTS,
)

cc_stage2_static_library(
    name = "ucrt_extra",
    deps = [":ucrtbase_crt_lib"],
)

# Mirror mingw-w64's libucrtbase import lib assembly: merge the DEF-based
# import library with the extra object archive. Using llvm-ar directly matches
# the MRI result for this simple merge.
run_binary(
    name = "ucrtbase",
    srcs = [
        "@mingw//:import_lib_ucrtbase",
        ":ucrt_extra",
    ],
    outs = ["libucrtbase.a"],
    tool = "//tools:llvm-ar",
    args = [
        "qcsDL",
        "$(location libucrtbase.a)",
        "$(location @mingw//:import_lib_ucrtbase)",
        "$(location ucrt_extra)",
    ],
    visibility = ["//visibility:public"],
)

# Debug-flavor import library assembled the same way as upstream (ucrtbased.mri).
run_binary(
    name = "ucrtbased",
    srcs = [
        "@mingw//:import_lib_ucrtbased",
        ":ucrt_extra",
    ],
    outs = ["libucrtbased.a"],
    tool = "//tools:llvm-ar",
    args = [
        "qcsDL",
        "$(location libucrtbased.a)",
        "$(location @mingw//:import_lib_ucrtbased)",
        "$(location ucrt_extra)",
    ],
    visibility = ["//visibility:public"],
)

# Assemble the umbrella UCRT import library like upstream's ucrt.mri recipe.
run_binary(
    name = "ucrt",
    srcs = [
        "@mingw//:import_lib_api-ms-win-crt-conio-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-convert-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-environment-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-filesystem-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-heap-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-locale-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-math-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-multibyte-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-private-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-process-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-runtime-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-stdio-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-string-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-time-l1-1-0",
        "@mingw//:import_lib_api-ms-win-crt-utility-l1-1-0",
        ":ucrt_extra",
    ],
    outs = ["libucrt.a"],
    tool = "//tools:llvm-ar",
    args = [
        "qcsDL",
        "$(location libucrt.a)",
        "$(location @mingw//:import_lib_api-ms-win-crt-conio-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-convert-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-environment-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-filesystem-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-heap-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-locale-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-math-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-multibyte-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-private-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-process-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-runtime-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-stdio-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-string-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-time-l1-1-0)",
        "$(location @mingw//:import_lib_api-ms-win-crt-utility-l1-1-0)",
        "$(location ucrt_extra)",
    ],
    visibility = ["//visibility:public"],
)

cc_stage2_library(
    name = "moldname_crt_lib",
    srcs = ["@mingw//:moldname_crt_srcs"],
    copts = MINGW_CRT_COMMON_COPTS,
)

cc_stage2_static_library(
    name = "moldname",
    deps = [":moldname_crt_lib"],
)

cc_stage2_library(
    name = "uuid_lib",
    srcs = ["@mingw//:mingw_uuid_srcs"],
    implementation_deps = [
        "@mingw//:mingw_headers",
    ],
    copts = MINGW_CRT_COMMON_COPTS,
)

cc_stage2_static_library(
    name = "uuid",
    deps = [":uuid_lib"],
)

# TODO(zbarsky): Hack for now until we have real libstdc++ support...
stub_library(
    name = "stdc++",
)

copy_to_directory(
    name = "mingw_crt_library_search_directory",
    srcs = [
        ":mingw32",
        ":mingwex",
        ":moldname",
        ":stdc++",
        ":ucrt",
        ":ucrtbase",
        ":ucrtbased",
        ":uuid",
    ],
    replace_prefixes = {
        "mingw32_": "",
        "mingwex_": "",
        "moldname_": "",
        "uuid_": "",
    },
    visibility = ["//visibility:public"],
    root_paths = [
        ".",
        "windows",
    ],
)

MINGW_CRT_OBJECT_COPTS = MINGW_CRT_COMMON_COPTS + [
    "-D_SYSCRT=1",
]

crt_object(
    name = "mingw_crtbegin",
    out = "crtbegin.o",
    srcs = ["@mingw//:mingw-w64-crt/crt/crtbegin.c"],
    copts = MINGW_CRT_OBJECT_COPTS,
    visibility = ["//visibility:public"],
)

crt_object(
    name = "mingw_crtend",
    srcs = ["@mingw//:mingw-w64-crt/crt/crtend.c"],
    out = "crtend.o",
    textual_hdrs = [
        "@mingw//:mingw_headers",
    ],
    copts = MINGW_CRT_OBJECT_COPTS,
    visibility = ["//visibility:public"],
)

crt_object(
    name = "mingw_crt2",
    out = "crt2.o",
    srcs = ["@mingw//:mingw-w64-crt/crt/crtexe.c"],
    implementation_deps = [
        "@mingw//:mingw_headers",
        "@mingw//:mingw_crt_headers",
    ],
    copts = MINGW_CRT_OBJECT_COPTS,
    visibility = ["//visibility:public"],
)

crt_object(
    name = "mingw_dllcrt2",
    out = "dllcrt2.o",
    srcs = ["@mingw//:mingw-w64-crt/crt/crtdll.c"],
    implementation_deps = [
        "@mingw//:mingw_headers",
        "@mingw//:mingw_crt_headers",
    ],
    copts = MINGW_CRT_OBJECT_COPTS,
    visibility = ["//visibility:public"],
)

copy_file(
    name = "version_lib",
    src = select({
        "@platforms//cpu:x86_64": "@mingw//:import_lib_x86_64_version",
        "@platforms//cpu:aarch64": "@mingw//:import_lib_aarch64_version",
    }),
    out = "version.lib",
    visibility = ["//visibility:public"],
)
