load("@bazel_skylib//rules:common_settings.bzl", "bool_setting")
load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains:artifacts.bzl", "cc_artifact_name_pattern")
load("@rules_cc//cc/toolchains/impl:documented_api.bzl", "cc_args_list")
load(":declare_toolchains.bzl", "declare_all_toolchains")

bool_setting(
    name = "bootstrap_setting",
    build_setting_default = False,
)

config_setting(
    name = "bootstrapped",
    flag_values = {
        ":bootstrap_setting": "0",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "bootstrapping",
    flag_values = {
        ":bootstrap_setting": "1",
    },
    visibility = ["//visibility:public"],
)

bool_setting(
    name = "stage1_bootstrap_setting",
    build_setting_default = False,
)

config_setting(
    name = "stage1_bootstrapped",
    flag_values = {
        ":stage1_bootstrap_setting": "0",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "stage1_bootstrapping",
    flag_values = {
        ":stage1_bootstrap_setting": "1",
    },
    visibility = ["//visibility:public"],
)

cc_args_list(
    name = "default_link_flags",
    args = select({
        "@platforms//os:macos": [
            "//toolchain/args/macos:macos_default_link_flags",
        ],
        "@platforms//os:windows": [],
        "//conditions:default": [],
    }),
)

cc_args_list(
    name = "default_startfiles",
    args = select({
        "@platforms//os:macos": [],
        "@platforms//os:linux": [
            "//toolchain/args:crt_search_directory",
        ],
        "@platforms//os:windows": [
            "//toolchain/args:crt_search_directory",
        ],
        "@platforms//os:none": [],
    }),
)

# For correctness.
#
# For later: this makes the clang driver use compiler-rt compliant filenames
# and paths when resolving compiler runtime libraries from the resource-dir.
#
# TODO(cerisier): Generate a custom resource-dir.
cc_args(
    name = "rtlib",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-rtlib=compiler-rt",
    ],
)

cc_args_list(
    name = "default_libs",
    args = select({
        "@platforms//os:macos": [
            "//toolchain/args/macos:macos_default_libs",
        ],
        "@platforms//os:linux": [
            "//toolchain/args:default_libs",
            "//toolchain/args/linux:default_libs",
        ],
        "@platforms//os:windows": [
            "//toolchain/args:default_libs",
            "//toolchain/args/windows:default_libs",
        ],
        "//conditions:default": [],
    }),
)

cc_args_list(
    name = "macos_toolchain_args",
    args = [
        "//toolchain/args/macos:macos_sdk_sysroot",
        "//toolchain/args/macos:macos_minimum_os_flags",
    ],
)

cc_args_list(
    name = "linux_toolchain_args",
    args = [
        "//toolchain/args/linux:hermetic_linux_args",
        "//toolchain/args:libcxx_headers_include_search_paths",
    ] + select({
        "//platforms/config/libc_aware:musl": [
            "//toolchain/args/linux:kernel_headers_include_search_paths",
            "//toolchain/args/linux:musl_libc_headers_include_search_paths",
        ],
        "//platforms/config/libc_aware:gnu": [
            "//toolchain/args/linux:kernel_headers_include_search_paths",
            "//toolchain/args/linux:glibc_headers_include_search_paths",
        ],
        #TODO: Remove once we have set the default glibc for a given target
        "//constraints/libc:unconstrained": [
            "//toolchain/args/linux:kernel_headers_include_search_paths",
            "//toolchain/args/linux:glibc_headers_include_search_paths",
        ],
        "//conditions:default": [],
    }) + select({
        "//platforms/config/libc_aware:musl": [
            #TODO: Handle musl dynamic linking
            # musl implies static linking for now
            "//toolchain/args:static_link_executable",
        ],
        "//conditions:default": [],
    }),
)

cc_args_list(
    name = "windows_toolchain_args",
    args = [
        "//toolchain/args:hermetic_compile_flags",
        "//toolchain/args:libcxx_headers_include_search_paths",
        "//toolchain/args/windows:mingw_headers_include_search_paths",
    ],
)

cc_args(
    name = "verbose",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        # "-v",
    ],
)

cc_args_list(
    name = "platform_specific_args",
    args = select({
        # Platform specific flags.
        "@platforms//os:macos": [":macos_toolchain_args"],
        "@platforms//os:linux": [":linux_toolchain_args"],
        "@platforms//os:windows": [":windows_toolchain_args"],
        "@platforms//os:none": [],
        "//conditions:default": [],
    }),
)

cc_args_list(
    name = "toolchain_args",
    args = [
        # Common default compile flags.
        "//toolchain/args:llvm_target_for_platform",
        "//toolchain/args:no_absolute_paths_for_builtins",
        "//toolchain/args:deterministic_compile_flags",
        # ":verbose",

        ":platform_specific_args",

        # Optional sanitizers
        "//toolchain/args:ubsan_flags",

        # Common default link flags.
        "//toolchain/args:use_lld",
        ":rtlib",
        ":default_link_flags",
        ":default_startfiles",
        ":default_libs",
    ],
    visibility = ["//toolchain:__subpackages__"],
)

cc_artifact_name_pattern(
    name = "windows_executable_pattern",
    category = "@rules_cc//cc/toolchains/artifacts:executable",
    prefix = "",
    extension = ".exe",
    visibility = ["//toolchain:__subpackages__"],
)

declare_all_toolchains()
