
load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains/impl:documented_api.bzl", "cc_args_list")
load("@rules_cc//cc/toolchains:actions.bzl", "cc_action_type_set")

package(default_visibility = ["//visibility:public"])

# TODO(zbarsky): Remove this when ugprading rules_cc
cc_action_type_set(
    name = "source_compile_actions",
    actions = [
        "@rules_cc//cc/toolchains/actions:linkstamp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_header_parsing",
        "@rules_cc//cc/toolchains/actions:cpp_module_compile",
        "@rules_cc//cc/toolchains/actions:cpp_module_codegen",
        "@rules_cc//cc/toolchains/actions:clif_match",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
        "@rules_cc//cc/toolchains/actions:preprocess_assemble",
        "@rules_cc//cc/toolchains/actions:c_compile_actions",
        "@rules_cc//cc/toolchains/actions:assembly_actions",
        "@rules_cc//cc/toolchains/actions:objc_compile",
    ],
)

# Passing a sysroot to /dev/null has the side effect of removing all default
# library search paths.
cc_args(
    name = "linux_no_sysroot",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "--sysroot=/dev/null",
    ],
)

cc_args(
    name = "hermetic_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-nodefaultlibs",
    ],
)

config_setting(
    name = "empty_sysroot",
    flag_values = {
        "//config:empty_sysroot": "True",
    },
)

cc_args_list(
    name = "hermetic_linux_args",
    args = select({
        ":empty_sysroot": [
            ":linux_no_sysroot",
        ],
        "//conditions:default": [],
    }) + [
        "//toolchain/args:hermetic_compile_flags",
        ":hermetic_link_flags",
    ],
)

cc_args(
    name = "kernel_headers_include_search_paths",
    actions = [
        ":source_compile_actions",
    ],
    args = [
        "-isystem",
        "{kernel_headers_include_search_path}",
    ],
    format = {
        "kernel_headers_include_search_path": "@kernel_headers//:kernel_headers_directory",
    },
    data = [
        "@kernel_headers//:kernel_headers_directory",
    ],
)

cc_args(
    name = "glibc_headers_include_search_paths",
    actions = [
        ":source_compile_actions",
    ],
    args = [
        # "__GLIBC_MINOR__={d}", version.minor
        "-isystem",
        "{libc_headers_include_search_path}",
    ],
    format = {
        "libc_headers_include_search_path": "//runtimes/glibc:glibc_headers_include_search_directory",
    },
    data = [
        "//runtimes/glibc:glibc_headers_include_search_directory",
    ],
)

cc_args(
    name = "default_libs_gnu",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        # We need to support copts like -lc -ldl etc...
        "-L{libc_library_search_path}",

        # In prevention of when all of those can be shared libraries.
        "-Wl,--push-state",
        "-Wl,--as-needed",

        # In newer versions of glibc various libs such as libm, librt are
        # normally included in libc and not available as separate libraries.
        #
        # But in the way we handle generating libc stubs, we chose to generate
        # empty libs for those that have been removed so that we do not have to
        # handle varying inclusions.
        "-lm",
        "-lpthread",
        "-lc",
        "-ldl",
        "-lrt",
        "-lutil",

        "-Wl,--pop-state",
    ],
    data = [
        "//runtimes/glibc:glibc_library_search_directory",
    ],
    format = {
        "libc_library_search_path": "//runtimes/glibc:glibc_library_search_directory",
    },
)

###

cc_args(
    name = "musl_libc_headers_include_search_paths",
    actions = [
        ":source_compile_actions",
    ],
    args = [
        "-isystem",
        "{libc_headers_include_search_path}",
    ],
    format = {
        "libc_headers_include_search_path": "//runtimes/musl:musl_headers_include_search_directory",
    },
    data = [
        "//runtimes/musl:musl_headers_include_search_directory",
    ],
)

#TODO: Factorize this and gnu
cc_args(
    name = "default_libs_musl",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        # We need to support copts like -lc -ldl etc...
        "-L{libc_library_search_path}",

        # In prevention of when all of those can be shared libraries.
        "-Wl,--push-state",
        "-Wl,--as-needed",

        # In newer versions of musl_libc various libs such as libm, librt are
        # normally included in libc and not available as separate libraries.
        #
        # But in the way we handle generating libc stubs, we chose to generate
        # empty libs for those that have been removed so that we do not have to
        # handle varying inclusions.
        "-lm",
        "-lpthread",
        "-lc",
        "-ldl",
        "-lrt",
        "-lutil",

        "-Wl,--pop-state",
    ],
    data = [
        "//runtimes/musl:musl_library_search_directory",
    ],
    format = {
        "libc_library_search_path": "//runtimes/musl:musl_library_search_directory",
    },
)

alias(
    name = "default_libs",
    actual = select({
        "//constraints/libc:musl": ":default_libs_musl",
        "//platforms/config/libc_aware:gnu": ":default_libs_gnu",
        #TODO: Remove once we have set the default glibc for a given target
        "//constraints/libc:unconstrained": ":default_libs_gnu",
    }),
)
