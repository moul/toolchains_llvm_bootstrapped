load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains/impl:documented_api.bzl", "cc_args_list")
load("//toolchain/args:llvm_target_triple.bzl", "LLVM_TARGET_TRIPLE")

package(default_visibility = ["//visibility:public"])

# COMMON RESET FLAGS
# --no-default-config
# -fno-spell-checking

    # if (target.cpu.arch.isArm()) {
    #     try argv.append(if (target.cpu.arch.isThumb()) "-mthumb" else "-mno-thumb");
    # }

# -mcpu for arm assembly 

cc_args(
    name = "resource_dir",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
        # We may need it for other actions too?
    ],
    args = [
        "-resource-dir",
        "{resource_dir}",
    ],
    format = {
        "resource_dir": "//runtimes:resource_directory",
    },
    data = [
        "//runtimes:resource_directory",
    ],
)

cc_args(
    name = "llvm_target_for_platform",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
)

cc_args(
    name = "libcxx_headers_include_search_paths",
    actions = [
        # cpp_compile_actions - lto_backend
        # Omitted to avoid unused arg warning for -I paths.
        "@rules_cc//cc/toolchains/actions:linkstamp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_header_parsing",
        "@rules_cc//cc/toolchains/actions:cpp_module_compile",
        "@rules_cc//cc/toolchains/actions:cpp_module_codegen",
        "@rules_cc//cc/toolchains/actions:clif_match",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        "-isystem",
        "{libcxx_headers_include_search_path}",
        "-isystem",
        "{libcxxabi_headers_include_search_path}",
    ],
    format = {
        "libcxx_headers_include_search_path": "//runtimes/libcxx:libcxx_headers_include_search_directory",
        "libcxxabi_headers_include_search_path": "//runtimes/libcxx:libcxxabi_headers_include_search_directory",
    },
    data = [
        "//runtimes/libcxx:libcxx_headers_include_search_directory",
        "//runtimes/libcxx:libcxxabi_headers_include_search_directory",
    ],
)

cc_args(
    name = "crt_search_directory",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
        "@rules_cc//cc/toolchains/actions:lto_index_for_executable",
        "@rules_cc//cc/toolchains/actions:lto_index_for_dynamic_library",
        "@rules_cc//cc/toolchains/actions:lto_index_for_nodeps_dynamic_library",
    ],
    # clang::driver::Toolchain::GetFilePath("crtn.o") looks for those using
    # prefix directories (-B).
    args = [
        "-B{crt_objects_directory}",
    ],
    format = {
        "crt_objects_directory": "//runtimes:crt_objects_directory",
    },
    data = [
        "//runtimes:crt_objects_directory",
    ],
)

cc_args(
    name = "static_link_executable",
    actions = [
        "@rules_cc//cc/toolchains/actions:cpp_link_executable",
        "@rules_cc//cc/toolchains/actions:lto_index_for_executable",
        "@rules_cc//cc/toolchains/actions:lto_index_for_dynamic_library",
        "@rules_cc//cc/toolchains/actions:lto_index_for_nodeps_dynamic_library",
    ],
    args = [
        "-static-pie",
    ],
)

cc_args(
    name = "force_linker_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-fuse-ld=lld",
        # lld will be used if it's next to clang.
        # It would have been great to set the path explicitly.
        # "--ld-path={lld_path}",
    ],
)

cc_args(
    name = "no_absolute_paths_for_builtins",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        # If the compiler sometimes rewrites paths in the .d files without symlinks
        # (ie when they're shorter), it confuses Bazel's logic for verifying all
        # #included header files are listed as inputs to the action.
        "-no-canonical-prefixes",
    ],
)

cc_args(
    name = "deterministic_compile_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        # Make C++ compilation deterministic. Use linkstamping instead of these
        # compiler symbols.
        "-Wno-builtin-macro-redefined",
        "-D__DATE__=\"redacted\"",
        "-D__TIMESTAMP__=\"redacted\"",
        "-D__TIME__=\"redacted\"",
        "-ffile-compilation-dir=.",  # Avoid absolute paths in binaries
    ],
)

cc_args(
    name = "ubsan_compiler_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-fno-omit-frame-pointer",
        "-fsanitize=undefined",
        "-fsanitize-ignorelist={ubsan_ignore}",
    ],
    format = {
        "ubsan_ignore": "//sanitizers:ubsan_ignore",
    },
    data = ["//sanitizers:ubsan_ignore"],
)

# Note that sanitizers must use `cc_unsanitized_library` on third-party deps
# if they end up depending on an exec-configure binary, as linking that binary can fail
# since it was compiled with sanitizer support but linked without it.
cc_args(
    name = "ubsan_linker_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "{ubsan_path}",
    ],
    format = {
        "ubsan_path": "@compiler-rt//:ubsan.static",
    },
    data = ["@compiler-rt//:ubsan.static"],
)

alias(
    name = "ubsan_enabled",
    actual = select({
        "//toolchain:stage1_bootstrapping": "//config/stage1:ubsan_enabled",
        "//conditions:default": "//config:ubsan_enabled",
    }),
    visibility = ["//toolchain:__subpackages__"],
)

cc_args_list(
    name = "ubsan_flags",
    args = select({
        ":ubsan_enabled": [
            ":ubsan_compiler_flags",
            ":ubsan_linker_flags",
        ],
        "//conditions:default": [],
    }),
)

cc_args(
    name = "hermetic_compile_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:cpp_compile_actions",
        "@rules_cc//cc/toolchains/actions:c_compile_actions",
        # -nostdlibinc triggers a warning for assemble (no preprocess)
        "@rules_cc//cc/toolchains/actions:preprocess_assemble",
        "@rules_cc//cc/toolchains/actions:objc_compile",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        # We want to disable everything except builtin headers since they are
        # provided as part of the compiler toolchain repository.
        "-nostdlibinc",
    ],
)


config_setting(
    name = "empty_sysroot",
    flag_values = {
        "//config:empty_sysroot": "True",
    },
)

# Passing a sysroot to /dev/null has the side effect of removing all default
# library search paths that clang passes like -L/usr/lib.
cc_args(
    name = "empty_sysroot_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = select({
        ":empty_sysroot": [
            "--sysroot=/dev/null",
        ],
        "//conditions:default": [],
    }),
)

cc_args(
    # We provide the libc++ and libunwind ourselves through static_runtime_libs
    # and dynamic_runtime_libs.
    name = "hermetic_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        # will prevent clang to choose which libunwind to link against
        "--unwindlib=none",
        # will prevent clang to choose which libc++ to link against
        "-nostdlib++",
    ],
)

cc_args(
    name = "default_libs",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-L{libcxx_library_search_path}",
        "-L{libunwind_library_search_path}",
    ],
    data = [
        "//runtimes/libcxx:libcxx_library_search_directory",
        "//runtimes/libunwind:libunwind_library_search_directory",
    ],
    format = {
        "libcxx_library_search_path": "//runtimes/libcxx:libcxx_library_search_directory",
        "libunwind_library_search_path": "//runtimes/libunwind:libunwind_library_search_directory",
    },
)

# This makes the clang driver use compiler-rt compliant filenames
# and paths when resolving compiler runtime libraries from the resource-dir.
cc_args(
    name = "rtlib",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-rtlib=compiler-rt",
    ],
)
