load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains/args:sysroot.bzl", "cc_sysroot")
load(
    ":macos_minimum_os_flag.bzl",
    "MACOS_MINIMUM_OS_VERSIONS",
    "macos_minimum_os_flag",
)

package(default_visibility = ["//visibility:public"])

cc_sysroot(
    name = "macos_sdk_sysroot",
    allowlist_include_directories = [
        "@macosx15.4.sdk//sysroot",
    ],
    data = [
        "@macosx15.4.sdk//sysroot",
    ],
    sysroot = "@macosx15.4.sdk//sysroot",
)

#TODO(cerisier): Fix with new cc_args BuildSettingInfo formatter once it is ready.
#                See https://github.com/cerisier/toolchains_llvm_bootstrapped/issues/302
macos_minimum_os_flag(
    name = "macos_minimum_os_flag",
)

[
    config_setting(
        name = "macos_minimum_os_{}".format(version.replace(".", "_")),
        flag_values = {
            ":macos_minimum_os_flag": version,
        },
    )
    for version in MACOS_MINIMUM_OS_VERSIONS
]

cc_args(
    name = "macos_minimum_os_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = select({
        ":macos_minimum_os_{}".format(version.replace(".", "_")): [
            "-mmacosx-version-min={}".format(version),
        ]
        for version in MACOS_MINIMUM_OS_VERSIONS
    } | {
        "//conditions:default": [
            # Keep current default behavior when the Apple minimum OS is not specified.
            "-mmacosx-version-min=14.0",
        ],
    }),
)

# TODO(cerisier): Extract those into proper semantic args list.
cc_args(
    name = "default_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-headerpad_max_install_names",
        "-Wl,-no_warn_duplicate_libraries",
        "-Wl,-oso_prefix,.",  # Strip absolute paths from debug info file references
    ],
    env = {
        # Required for hermetic links on macOS
        "ZERO_AR_DATE": "1",
    },
)

cc_args(
    name = "default_libs",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    # -l: is not supported by lld64
    args = [
        "{libclang_rt.builtins.a}",
        "-lSystem",
    ],
    data = [
        "//runtimes/compiler-rt:clang_rt.builtins.static",
    ],
    format = {
        "libclang_rt.builtins.a": "//runtimes/compiler-rt:clang_rt.builtins.static",
    },
)
