
load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains/args:sysroot.bzl", "cc_sysroot")

package(default_visibility = ["//visibility:public"])

cc_sysroot(
    name = "macos_sdk_sysroot",
    sysroot = "@macosx15.4.sdk//:sysroot-minimal",
    data = [
        "@macosx15.4.sdk//:sysroot-minimal",
    ],
)

cc_args(
    name = "macos_minimum_os_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        #TODO: This should be configurable or retrieved from the apple fragment
        "-mmacosx-version-min=14.0",
    ],
)

cc_args(
    name = "macos_default_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-headerpad_max_install_names",
        "-Wl,-no_warn_duplicate_libraries",
        "-Wl,-oso_prefix,.",  # Strip absolute paths from debug info file references
    ],
    env = {
        # Required for hermetic links on macOS
        "ZERO_AR_DATE": "1",
    },
)

cc_args(
    name = "macos_default_libs",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    # -l: is not supported by lld64
    args = [
        # provides a stub for -lunwind on macOS
        "-L{libunwind_library_search_path}",

        "{libclang_rt.builtins.a}",
        "-lSystem",
    ],
    format = {
        "libclang_rt.builtins.a": "//runtimes/compiler-rt:clang_rt.builtins.static",
        "libunwind_library_search_path": "//runtimes/libunwind:libunwind_library_search_directory",
    },
    data = [
        "//runtimes/compiler-rt:clang_rt.builtins.static",
        "//runtimes/libunwind:libunwind_library_search_directory",
    ],
)

