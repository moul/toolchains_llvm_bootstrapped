load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains/impl:documented_api.bzl", "cc_args_list")

package(default_visibility = ["//toolchain/stage2:__subpackages__"])

# compile_actions minus assemble_actions (only preprocess_assemble)
OPT_FLAGS_COMPILE_ACTIONS = [
    "@rules_cc//cc/toolchains/actions:cpp_compile_actions",
    "@rules_cc//cc/toolchains/actions:c_compile_actions",
    "@rules_cc//cc/toolchains/actions:preprocess_assemble",
    "@rules_cc//cc/toolchains/actions:objc_compile",
    "@rules_cc//cc/toolchains/actions:objcpp_compile",
]

# libc must be compiled with optimizations (should match with above)
cc_args(
    name = "opt_compile_flags",
    # compile_actions minus assemble_actions (only preprocess_assemble)
    actions = OPT_FLAGS_COMPILE_ACTIONS,
    args =  [
        "-DNDEBUG",
        "-O2",
    ],
)

cc_args(
    name = "dbg_compile_flags",
    actions = OPT_FLAGS_COMPILE_ACTIONS,
    args = [
        "-D_DEBUG",
        "-O0",
        "-g",
    ],
)

cc_args_list(
    name = "optimization_flags",
    args = select({
        "//config:stage2_optimization_mode_debug": [
            ":dbg_compile_flags",
        ],
        "//conditions:default": [
            ":opt_compile_flags",
        ],
    }),
)

cc_args_list(
    name = "ubsan_flags",
    args = select({
        # We just need to recompile with sanitizer flags, don't need linker flags.
        # And in fact linker flags will cause a cycle. If we ever need a sanitizer
        # for compilation, we would need an extra bootstrap stage.
        "//toolchain/args:ubsan_enabled": [
            "//toolchain/args:ubsan_compiler_flags",
        ],
        "//conditions:default": [],
    }),
)

# TODO(cerisier): this needs to differ per runtime (e.g libc++ still need crtbegin)
cc_args(
    name = "hermetic_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-nostdlib",
    ],
)
